load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@aspect_bazel_lib//lib:testing.bzl", "assert_archive_contains")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load(":asserts.bzl", "assert_tar_listing")

write_file(
    name = "gen_a",
    out = "a",
    content = ["hello a"],
)

genrule(
    name = "tar_a",
    srcs = ["a"],
    outs = ["a.tar"],
    cmd = "$(BSDTAR_BIN) -C $(BINDIR) --create --dereference --file $@ $(rootpath a)",
    toolchains = ["@bsd_tar//:resolved_toolchain"],
)

assert_archive_contains(
    name = "check_a",
    archive = "a.tar",
    expected = ["lib/tests/tar/a"],
)

# Example from https://man.freebsd.org/cgi/man.cgi?query=bsdtar&sektion=1&format=html#end
write_file(
    name = "input_mtree",
    out = "input.mtree",
    content = [
        "usr/bin uid=0 gid=0 mode=0755 type=dir",
        "usr/bin/ls uid=0 gid=0 mode=0755 time=0 type=file content=lib/tests/tar/a.tar",
        "",
    ],
)

_MTREE_EXPECTED = [
    "drwxr-xr-x  0 0      0           0 Dec 31  1969 usr/bin/",
    "-rwxr-xr-x  0 0      0        2048 Dec 31  1969 usr/bin/ls",
]

genrule(
    name = "tar_b",
    srcs = [
        "input.mtree",
        "tar_a",
    ],
    outs = ["b.tar"],
    cmd = "$(BSDTAR_BIN)  --cd $(BINDIR) --create --file $@ @$(rootpath input.mtree)",
    toolchains = ["@bsd_tar//:resolved_toolchain"],
)

assert_tar_listing(
    name = "test_b",
    actual = "b.tar",
    expected = _MTREE_EXPECTED,
)

tar(
    name = "c",
    srcs = ["a.tar"],
    mtree = ":input_mtree",
)

assert_tar_listing(
    name = "test_c",
    actual = "c.tar",
    expected = _MTREE_EXPECTED,
)
