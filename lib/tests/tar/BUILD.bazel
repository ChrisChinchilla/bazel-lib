load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@aspect_bazel_lib//lib:testing.bzl", "assert_archive_contains")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load(":asserts.bzl", "assert_tar_listing")

write_file(
    name = "gen_a",
    out = "a",
    content = ["hello a"],
)

# Show that you can run any `tar` command you like using a genrule.
genrule(
    name = "tar_a",
    srcs = ["a"],
    outs = ["a.tar"],
    cmd = "$(BSDTAR_BIN) -C $(BINDIR) --create --dereference --file $@ $(rootpath a)",
    toolchains = ["@bsd_tar//:resolved_toolchain"],
)

assert_archive_contains(
    name = "check_a",
    archive = "a.tar",
    expected = ["lib/tests/tar/a"],
)

# Demonstrate using an mtree formatted specification file.
# Example from https://man.freebsd.org/cgi/man.cgi?query=bsdtar&sektion=1&format=html#end
write_file(
    name = "input_mtree",
    out = "input.mtree",
    content = [
        "usr/bin uid=0 gid=0 mode=0755 type=dir",
        "usr/bin/ls uid=0 gid=0 mode=0755 time=0 type=file content={}/a".format(package_name()),
        "",
    ],
)

# We expect the resulting `tar -tvf` to be the following:
_MTREE_EXPECTED = [
    "drwxr-xr-x  0 0      0           0 Dec 31  1969 usr/bin/",
    "-rwxr-xr-x  0 0      0           7 Dec 31  1969 usr/bin/ls",
]

tar(
    name = "b",
    srcs = ["a"],
    mtree = ":input_mtree",
)

assert_tar_listing(
    name = "test_b",
    actual = "b",
    expected = _MTREE_EXPECTED,
)
